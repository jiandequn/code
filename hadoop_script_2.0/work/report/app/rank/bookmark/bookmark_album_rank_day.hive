set hive.exec.dynamic.partition=true;
set hive.exec.dynamic.partition.mode=nonstrict;
set hive.groupby.orderby.position.alias=true; -- 启用别名对于groupby和orderby
set hive.vectorized.execution.enabled=false; --控制是否启用查询执行的向量模式

-- 媒资收藏排行前10
-- 用户收藏记录操作（新增和删除）信息 op_state 1表示新增，-1表示取消 存储每日的信息好跟踪
truncate table app_bookmark_rank;
insert overwrite table app_bookmark_rank
select t.t_date,t.parent_column_id,t.album_id,am.album_name,am.content_type,act.content_type_name,t.count from(
select '${endDate}' as t_date,parent_column_id,album_id,sum(op_state) count from(
select parent_column_id ,user_type,user_id,param["contentId"] as album_id,to_unix_timestamp(create_time) as time_stamp,if(param["operateType"]='add',1,-1) as op_state,concat(y,'-',m,'-',d) as t_date from events_type_log
where events_type ='COLUMN_TYPE_BOOK_MARK' and concat(y,'-',m,'-',d)<'${endDate}' and param["operateType"] in('add','del'))
 a group by parent_column_id,album_id order by count desc limit 20) t
 left join album am on am.album_id=t.album_id
 left join album_content_type act on act.content_type=am.content_type;

create TEMPORARY table app_bookmark_rank_day_tmp as
select a1.t_date,a1.parent_column_id,a1.album_id,sum(a1.op_state) as count from(
select a.t_date,a.parent_column_id,a.user_type,a.user_id,a.album_id,sum(op_state) op_state from(
select parent_column_id ,user_type,user_id,param["contentId"] as album_id
,if(param["operateType"]='add',1,-1) as op_state,concat(y,'-',m,'-',d) as t_date
from events_type_log
where events_type ='COLUMN_TYPE_BOOK_MARK' and concat(y,'-',m,'-',d)>='${startDate}'
and concat(y,'-',m,'-',d)<'${endDate}' and param["operateType"] in('add','del'))a
group by a.t_date,a.parent_column_id,a.user_type,a.user_id,a.album_id)a1
where a1.op_state=1
group by a1.t_date,a1.parent_column_id,a1.album_id;


insert overwrite table app_bookmark_rank_day
select t.t_date,t.parent_column_id,t.album_id,a.album_name,a.content_type,act.content_type_name,t.count from(
select t_date,parent_column_id,album_id,count,
  dense_rank() OVER (partition by t_date,parent_column_id ORDER BY count desc) rank from app_bookmark_rank_day_tmp)t
 left join album a on a.album_id=t.album_id
 left join album_content_type act on act.content_type=a.content_type
  where t.rank<=20;





